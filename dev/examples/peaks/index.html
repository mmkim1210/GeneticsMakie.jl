<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>Plotting peaks · GeneticsMakie.jl</title><script data-outdated-warner src="../../assets/warner.js"></script><link rel="canonical" href="https://mmkim1210.github.io/GeneticsMakie.jl/examples/peaks/"/><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.045/juliamono.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.13.24/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL="../.."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../../assets/documenter.js"></script><script src="../../siteinfo.js"></script><script src="../../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><a class="docs-logo" href="../../"><img src="../../assets/logo.svg" alt="GeneticsMakie.jl logo"/></a><form class="docs-search" action="../../search/"><input class="docs-search-query" id="documenter-search-query" name="q" type="text" placeholder="Search docs"/></form><ul class="docs-menu"><li><a class="tocitem" href="../../">Home</a></li><li><span class="tocitem">Examples</span><ul><li><a class="tocitem" href="../gtf/">Parsing GENCODE</a></li><li><a class="tocitem" href="../summary/">Munging summary statistics</a></li><li><a class="tocitem" href="../genes/">Plotting genes</a></li><li><a class="tocitem" href="../isoforms/">Plotting isoforms</a></li><li><a class="tocitem" href="../locus/">Plotting LocusZoom</a></li><li><a class="tocitem" href="../gwas/">Plotting GWAS</a></li><li><a class="tocitem" href="../twas/">Plotting TWAS</a></li><li><a class="tocitem" href="../loops/">Plotting loops</a></li><li class="is-active"><a class="tocitem" href>Plotting peaks</a></li></ul></li><li><a class="tocitem" href="../../api/">API</a></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><nav class="breadcrumb"><ul class="is-hidden-mobile"><li><a class="is-disabled">Examples</a></li><li class="is-active"><a href>Plotting peaks</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>Plotting peaks</a></li></ul></nav><div class="docs-right"><a class="docs-edit-link" href="https://github.com/mmkim1210/GeneticsMakie.jl/blob/master/docs/src/examples/peaks.md#" title="Edit on GitHub"><span class="docs-icon fab"></span><span class="docs-label is-hidden-touch">Edit on GitHub</span></a><a class="docs-settings-button fas fa-cog" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-sidebar-button fa fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a></div></header><article class="content" id="documenter-page"><h1 id="Plotting-peaks"><a class="docs-heading-anchor" href="#Plotting-peaks">Plotting peaks</a><a id="Plotting-peaks-1"></a><a class="docs-heading-anchor-permalink" href="#Plotting-peaks" title="Permalink"></a></h1><p>Epigenetic sequencing is another data modality of interest that we can visualize with <a href="https://makie.juliaplots.org/stable/"><strong>Makie.jl</strong></a> and GeneticsMakie.jl. ChIP-seq and ATAC-seq map epigenetic modifications throughout the genome by identifying DNA-protein interaction sites and open chromatin respectively, and although they capture different aspects of the epigenome, their resulting data share the same form and function (i.e. BAM files to capture reads, wiggle files to capture coverage and signal, BED files to capture peaks). Consequently, visualizing both types of data will follow the same workflow.</p><p>We will start with the annotation from <a href="../gtf/#Parsing-GENCODE">Parsing GENCODE</a>. Accompanying ChIP-seq data will be downloaded from the <a href="https://www.encodeproject.org/">ENCODE project</a>.</p><pre><code class="language-julia hljs">using Pkg
Pkg.add([&quot;GeneticsMakie&quot;, &quot;CairoMakie&quot;, &quot;CSV&quot;, &quot;DataFrames&quot;, &quot;Arrow&quot;, &quot;BigWig&quot;])</code></pre><pre><code class="language-julia hljs">using GeneticsMakie, CairoMakie, CSV, DataFrames, Arrow, Downloads, BigWig
chipseq = Dict(
    &quot;continuous&quot; =&gt; &quot;https://www.encodeproject.org/files/ENCFF343VSH/@@download/ENCFF343VSH.bigWig&quot;,
    &quot;peaks&quot; =&gt; &quot;https://www.encodeproject.org/files/ENCFF938RRM/@@download/ENCFF938RRM.bed.gz&quot;
)
isdir(&quot;data/chipseq&quot;) || mkdir(&quot;data/chipseq&quot;)
for key in keys(chipseq)
    url = chipseq[key]
    isfile(&quot;data/chipseq/$(basename(url))&quot;) || Downloads.download(url, &quot;data/chipseq/$(basename(url))&quot;)
end
url = &quot;https://ftp.ebi.ac.uk/pub/databases/gencode/Gencode_human/release_39/GRCh37_mapping/gencode.v39lift37.annotation.gtf.gz&quot;
gencode = Arrow.Table(&quot;data/gencode/$(splitext(basename(url))[1]).arrow&quot;)|&gt; DataFrame</code></pre><p>We can generate a track visualizing ChIP-seq data continuously over a locus. Here we plot the the p-value from processing the ChiP-seq data through <a href="https://pypi.org/project/MACS2/">MACS2</a> for rejecting the null hypothesis that the signal at that site is also present in the control. Continuous data like this is often stored in bigWig files, which store dense, continuous data in indexed binary files. Other types of continuous data can be stored and delivered through this format; BAM files can be converted to the bigWig format in order to convey read coverage over the genome. We will use the <a href="https://biojulia.net/BigWig.jl/dev/"><strong>BigWig.jl</strong></a> package to read in this data to plot.</p><pre><code class="language-julia hljs">gene = &quot;KMT2E&quot;
chr, start, stop = GeneticsMakie.findgene(gene, gencode)
ranges = [start - 1e6, stop + 1e6]

f = Figure(resolution = (306, 792))
axs = [Axis(f[i, 1]) for i in 1:2]
# Generate continuous plot of signal p-values
let
    ax = axs[1]
    reader = open(BigWig.Reader, &quot;data/chipseq/$(basename(chipseq[&quot;continuous&quot;]))&quot;)
    xs = Float64[]
    ys = Float64[]
    for record in BigWig.GenomicFeatures.eachoverlap(reader,
        BigWig.Interval(&quot;chr$(chr)&quot;, Int64(ranges[1]), Int64(ranges[2])))
        # Extract position
        push!(xs, BigWig.chromstart(record))
        # Extract -log₁₀(p)
        push!(ys, BigWig.value(record))
    end
    band!(ax, xs, fill(0, length(xs)), ys; color = (&quot;#389826&quot;, 0.75))
    lines!(ax, xs, ys; color = (&quot;#389826&quot;, 0.75))
    rowsize!(f.layout, 1, 30)
    close(reader)
    Label(f[1, 1, Top()], &quot;H3K4me3&quot;, fontsize = 6, halign = :left, padding = (7.5, 0, -5, 0))
    # Tidy up the axes
    ymax = max(10, maximum(ys) * 1.25)
    ax.spinewidth = 0.75
    ax.ytickwidth = 0.75
    ax.ylabelsize = 6
    ax.yticklabelsize = 6
    ax.yticksize = 3
    if ymax == 10
        ax.yticks = 0:3:10
    elseif 10 &lt; ymax &lt;= 20
        ax.yticks = 0:5:ymax
    elseif 20 &lt; ymax &lt;= 50
        ax.yticks =  0:10:ymax
    else
        s = div(ymax, 4)
        d, r = divrem(s, 10)
        if r &lt; 5 
            ax.yticks = (0:10d:ymax)
        else
            ax.yticks = (0:((d + 1) * 10):ymax)
        end
    end
    xlims!(ax, ranges[1], ranges[2])
    ylims!(ax, 0, ymax)
    hidespines!(ax, :t, :r)
    hidexdecorations!(ax)
    hideydecorations!(ax, ticks = false, label = false, ticklabels = false)
end
# Plot genes and adjust formatting
rs = GeneticsMakie.plotgenes!(axs[2], chr, ranges[1], ranges[2], gencode; height = 0.1)
rowsize!(f.layout, 2, rs)
GeneticsMakie.labelgenome(f[2, 1, Bottom()], chr, ranges[1], ranges[2])
Label(f[1, 0], text = &quot;-log[p]&quot;, fontsize = 6, rotation = pi / 2)
rowgap!(f.layout, 5)
colgap!(f.layout, 5)
for i in 1:2
    vlines!(axs[i], start, color = (:gold, 0.5), linewidth = 0.5)
    vlines!(axs[i], stop, color = (:gold, 0.5), linewidth = 0.5)
end
resize_to_layout!(f)
f</code></pre><p><img src="../../figs/KMT2E-chip-seq-signal.png" alt/></p><p>An alternative way to visualize ChIP-seq data is by drawing the peaks only. There are various tools for calling peaks, and for the most part they output the peaks in a BED file or a similar format. Here we load in and draw peaks called by <a href="https://pypi.org/project/MACS2/">MACS2</a>.</p><pre><code class="language-julia hljs">using Makie.GeometryBasics
f = Figure(resolution = (306, 792))
axs = [Axis(f[i, 1]) for i in 1:2]
# Generate plot of peaks
let
    ax = axs[1]
    narrowpeak_header =
    [:chrom, :start, :stop, :name, :score, :strand,
     :signalvalue, :nlog₁₀pvalue, :nlog₁₀qvalue, :peak]
    df = CSV.read(&quot;data/chipseq/$(basename(chipseq[&quot;peaks&quot;]))&quot;, DataFrame;
                  header = narrowpeak_header)
    subset!(df,
            :chrom =&gt; col -&gt; col .== &quot;chr$(chr)&quot;,
            [:start, :stop] =&gt;
            (start, stop) -&gt;
            (start .&lt; ranges[2]) .&amp;&amp; (stop .&gt; ranges[1]))
    for row in eachrow(df)
        peakpoly = Polygon(Point2f[(row.start, 0),
                                   (row.start, 10),
                                   (row.stop, 10),
                                   (row.stop, 0)])
        poly!(ax, peakpoly; color = &quot;#CB3C33&quot;)
    end
    ax.spinewidth = 0.75
    hidexdecorations!(ax)
    hideydecorations!(ax)
    xlims!(ax, ranges[1], ranges[2])
    ylims!(ax, -10 * .25, 10 * 1.5)
    hidespines!(ax, :t, :r)
    rowsize!(f.layout, 1, 10)
end
# Plot genes and adjust formatting
rs = GeneticsMakie.plotgenes!(axs[2], chr, ranges[1], ranges[2], gencode; height = 0.1)
rowsize!(f.layout, 2, rs)
GeneticsMakie.labelgenome(f[2, 1, Bottom()], chr, ranges[1], ranges[2])
Label(f[1, 0], text = &quot;Peaks&quot;, fontsize = 6, rotation = pi / 2)
Label(f[0, 2], &quot;H3K4me3&quot;, fontsize = 6, halign = :left, padding = (7.5, 0, -5, 0), tellwidth = false)
rowgap!(f.layout, 5)
colgap!(f.layout, 5)
for i in 1:2
    vlines!(axs[i], start, color = (:gold, 0.5), linewidth = 0.5)
    vlines!(axs[i], stop, color = (:gold, 0.5), linewidth = 0.5)
end
resize_to_layout!(f)
f</code></pre><p><img src="../../figs/KMT2E-chip-seq-peaks.png" alt/></p><p>Using <a href="https://makie.juliaplots.org/stable/"><strong>Makie.jl</strong></a>&#39;s layout tools, we can visualize both the continuous track and the called peaks simultaneously.</p><pre><code class="language-julia hljs">f = Figure(resolution = (306, 792))
axs = [Axis(f[i, 1]) for i in 1:3]
# Generate continuous plot of signal p-values
let
    ax = axs[1]
    reader = open(BigWig.Reader, &quot;data/chipseq/$(basename(chipseq[&quot;continuous&quot;]))&quot;)
    xs = Float64[]
    ys = Float64[]
    for record in BigWig.GenomicFeatures.eachoverlap(reader,
        BigWig.Interval(&quot;chr$(chr)&quot;, Int64(ranges[1]), Int64(ranges[2])))
        # Extract position
        push!(xs, BigWig.chromstart(record))
        # Extract -log₁₀(p)
        push!(ys, BigWig.value(record))
    end
    band!(ax, xs, fill(0, length(xs)), ys; color = (&quot;#389826&quot;, 0.75))
    lines!(ax, xs, ys; color = (&quot;#389826&quot;, 0.75))
    rowsize!(f.layout, 1, 30)
    close(reader)
    Label(f[1, 1, Top()], &quot;H3K4me3&quot;, fontsize = 6, halign = :left, padding = (7.5, 0, -5, 0))
    # Tidy up the axes
    ymax = max(10, maximum(ys) * 1.25)
    ax.spinewidth = 0.75
    ax.ytickwidth = 0.75
    ax.ylabelsize = 6
    ax.yticklabelsize = 6
    ax.yticksize = 3
    if ymax == 10
        ax.yticks = 0:3:10
    elseif 10 &lt; ymax &lt;= 20
        ax.yticks = 0:5:ymax
    elseif 20 &lt; ymax &lt;= 50
        ax.yticks =  0:10:ymax
    else
        s = div(ymax, 4)
        d, r = divrem(s, 10)
        if r &lt; 5 
            ax.yticks = (0:10d:ymax)
        else
            ax.yticks = (0:((d + 1) * 10):ymax)
        end
    end
    xlims!(ax, ranges[1], ranges[2])
    ylims!(ax, 0, ymax)
    hidespines!(ax, :t, :r)
    hidexdecorations!(ax)
    hideydecorations!(ax, ticks = false, label = false, ticklabels = false)
end
# Generate plot of peaks
let
    ax = axs[2]
    narrowpeak_header =
    [:chrom, :start, :stop, :name, :score, :strand,
     :signalvalue, :nlog₁₀pvalue, :nlog₁₀qvalue, :peak]
    df = CSV.read(&quot;data/chipseq/$(basename(chipseq[&quot;peaks&quot;]))&quot;, DataFrame;
                  header = narrowpeak_header)
    subset!(df,
            :chrom =&gt; col -&gt; col .== &quot;chr$(chr)&quot;,
            [:start, :stop] =&gt;
            (start, stop) -&gt;
            (start .&lt; ranges[2]) .&amp;&amp; (stop .&gt; ranges[1]))
    for row in eachrow(df)
        peakpoly = Polygon(Point2f[(row.start, 0),
                                   (row.start, 10),
                                   (row.stop, 10),
                                   (row.stop, 0)])
        poly!(ax, peakpoly; color = &quot;#CB3C33&quot;)
    end
    ax.spinewidth = 0.75
    hidexdecorations!(ax)
    hideydecorations!(ax)
    xlims!(ax, ranges[1], ranges[2])
    ylims!(ax, -10 * .25, 10 * 1.5)
    hidespines!(ax, :t, :r)
    rowsize!(f.layout, 2, 10)
end
# Plot genes and adjust formatting
rs = GeneticsMakie.plotgenes!(axs[3], chr, ranges[1], ranges[2], gencode; height = 0.1)
rowsize!(f.layout, 3, rs)
GeneticsMakie.labelgenome(f[3, 1, Bottom()], chr, ranges[1], ranges[2])
Label(f[1, 0], text = &quot;-log[p]&quot;, fontsize = 6, rotation = pi / 2)
Label(f[2, 1], text = &quot;Peaks&quot;, fontsize = 6, rotation = pi / 2)
rowgap!(f.layout, 5)
colgap!(f.layout, 5)
for i in 1:3
    vlines!(axs[i], start, color = (:gold, 0.5), linewidth = 0.5)
    vlines!(axs[i], stop, color = (:gold, 0.5), linewidth = 0.5)
end
resize_to_layout!(f)
f</code></pre><p><img src="../../figs/KMT2E-chip-seq-signal-peaks.png" alt/></p></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../loops/">« Plotting loops</a><a class="docs-footer-nextpage" href="../../api/">API »</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> version 0.27.23 on <span class="colophon-date" title="Saturday 7 January 2023 08:53">Saturday 7 January 2023</span>. Using Julia version 1.8.4.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
